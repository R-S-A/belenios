(** ElGamal cryptographic operations *)

open Helios_datatypes_t

module type GROUP = sig
  type t
    (** type of elements *)

  val g : t
    (** generator *)

  val q : Z.t
    (** order of [g] *)

  val p : Z.t
    (** order of surrounding group *)

  val ( *~ ) : t -> t -> t
    (** multiplication *)

  val ( **~ ) : t -> Z.t -> t
    (** exponentiation *)

  val ( =~ ) : t -> t -> bool
    (** equality test *)

  val inv : t -> t
    (** inversion *)

  val check_element : t -> bool
    (** check group membership *)

  val hash : t list -> Z.t
    (** hash a list of elements into an integer mod [q] using SHA-1 *)
end
  (** Signature of an abstract group suitable for ElGamal. *)

val make_ff_msubgroup : p:Z.t -> q:Z.t -> g:Z.t -> (module GROUP with type t = Z.t)
  (** [make_ff_msubgroup p q g] builds the multiplicative subgroup of
      F[p], generated by [g], of order [q]. *)

module type ELGAMAL_CRYPTO = sig
  type t
  val verify_public_key : t public_key -> bool
  val verify_private_key : t private_key -> bool
  val verify_pok : t -> t pok -> bool
  val verify_election_key : t -> t trustee_public_key array -> bool
  val verify_disjunction : t -> t -> t array -> t proof array -> bool
  val verify_range : t -> int -> int -> t -> t -> t proof array -> bool
  val verify_answer : t -> question -> t answer -> bool
  val verify_ballot : t election -> string -> t ballot -> bool
  val verify_equality : t -> t -> t -> t proof -> bool
  val verify_partial_decryption : t election ->
    t tally -> t trustee_public_key -> t partial_decryption -> bool
  val verify_partial_decryptions : t election ->
    t trustee_public_key array -> t result -> bool
  val verify_result : t election -> t result -> bool
  val compute_encrypted_tally : t election -> t ballot array -> t encrypted_tally
end

module Make (G : GROUP) : ELGAMAL_CRYPTO with type t := G.t
